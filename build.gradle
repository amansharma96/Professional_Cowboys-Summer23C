import org.apache.tools.ant.filters.ReplaceTokens
import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

plugins {
  id 'java'
  id 'application'
  id 'jacoco'
}

mainClassName = 'memoranda/Start'

compileJava.options.encoding = 'UTF-8'

repositories {
  mavenCentral()
}

dependencies {
  implementation 'com.io7m.xom:xom:1.2.+'
  implementation 'net.sourceforge.nekohtml:nekohtml:1.9.+'
  implementation 'org.junit.jupiter:junit-jupiter:5.8.1'
}

sourceSets {
  test {
    java.srcDir 'src/test/java'
  }
}

jacoco {
  toolVersion = "0.8.9"
}

jacocoTestReport {
  reports {
    html.destination file("${buildDir}/jacocoHtml")
  }
  dependsOn test // this makes sure the test task is run before the report is generated
}

test {
  useJUnitPlatform()
  finalizedBy jacocoTestReport
}

// Get the current date and time as a string
def now = LocalDateTime.now()
def formatter = DateTimeFormatter.ofPattern("MM/dd/yy-HH:mm:ss")
def timestamp = now.format(formatter)

ext {
  version = '1.3.0'
  build = timestamp
}

processResources {
  filesMatching('version.properties') {
    filter ReplaceTokens, tokens: ['VERSION': project.ext.version, 'BUILD': project.ext.build]
  }
}
